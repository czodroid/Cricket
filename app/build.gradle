// Filename: build.gradle
// Author: Olivier Sirol <czo@free.fr>
// License: GPL-2.0 (http://www.gnu.org/copyleft)
// File Created: avril 2012
// Last Modified: Monday 12 May 2025, 18:29
// Edit Time: 1:57:56
// Description: app build.gradle

apply plugin: 'com.android.application'

println("")
def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
def canSign = false
if (keystorePropertiesFile.exists()) {
    canSign = true
    println("-> release will be signed.")
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

int minutesSinceEpoch = System.currentTimeMillis() / 1000 / 60
String buildDateYMD = new java.text.SimpleDateFormat("yyyyMMdd").format(new java.util.Date(System.currentTimeMillis()))

def gitVersionName = "git -C ${rootDir} describe --tags --long".execute().text.trim()

android {
    namespace 'org.czo.cricket'
    compileSdk 35
    defaultConfig {
        applicationId "org.czo.cricket"
        minSdkVersion 9
        targetSdkVersion 35
        versionCode minutesSinceEpoch

        println("-> gitVersionName:   " + gitVersionName)
        setProperty("versionName", "${gitVersionName}".replaceAll(/-.*/,''))
        println("-> versionName:      " + versionName)
        println("-> versionCode:      " + minutesSinceEpoch)
        setProperty("archivesBaseName", "${applicationId}-${buildDateYMD}")
        println("-> archivesBaseName: " + archivesBaseName)
        println("")
    }

    if (canSign) {
        signingConfigs {
            release {
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                v1SigningEnabled true
                v2SigningEnabled true
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if (canSign) {
                signingConfig signingConfigs.release
            }
        }
    }

    //tasks.withType(JavaCompile) { options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation" }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
}
